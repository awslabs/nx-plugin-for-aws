terraform {
  required_version = ">= 1.0"

  required_providers {
    aws = {
      source  = "hashicorp/aws"
      version = ">= 6.23"
    }
    null = {
      source  = "hashicorp/null"
      version = ">= 3.0"
    }
    random = {
      source  = "hashicorp/random"
      version = ">= 3.0"
    }
  }
}

# Variables
variable "agent_runtime_name" {
  description = "Name of the agent runtime"
  type        = string
  validation {
    condition     = can(regex("^[a-zA-Z][a-zA-Z0-9_]{0,42}$", var.agent_runtime_name))
    error_message = "Value must start with a letter and contain only letters, numbers, and underscores (1-43 characters)."
  }
}

variable "server_protocol" {
  description = "Server protocol for the agent runtime (HTTP, MCP, or A2A)"
  type        = string
  default     = "HTTP"
  validation {
    condition     = contains(["MCP", "HTTP", "A2A"], var.server_protocol)
    error_message = "Protocol type must be either 'MCP', 'HTTP', or 'A2A'."
  }
}

variable "authorizer_configuration" {
  description = "Authorization configuration for authenticating incoming requests"
  type = object({
    custom_jwt_authorizer = optional(object({
      discovery_url    = string
      allowed_audience = optional(list(string))
      allowed_clients  = optional(list(string))
    }))
  })
  default = null
}

variable "docker_image_tag" {
  description = "Name of the docker image tag to use as the agent core runtime"
  type        = string
}

variable "env" {
  description = "Environment variables to pass to the agent core runtime"
  type        = map(string)
  default     = {}
}

variable "additional_iam_policy_statements" {
  description = "Additional IAM policy statements to attach to the agent core runtime role"
  type = list(object({
    Effect   = string
    Action   = list(string)
    Resource = list(string)
  }))
  default = []
}

variable "tags" {
  description = "Tags to apply to resources"
  type        = map(string)
  default     = {}
}

# Data sources
data "aws_caller_identity" "current" {}
data "aws_region" "current" {}

locals {
  aws_account_id = data.aws_caller_identity.current.account_id
  aws_region     = data.aws_region.current.id
}

# Random ID for bucket suffix to ensure uniqueness
resource "random_id" "unique_suffix" {
  byte_length = 4
}

# ECR Repository
resource "aws_ecr_repository" "agent_core_repository" {
  #checkov:skip=CKV_AWS_136:AES256 encryption is sufficient for ECR repositories
  name = "${lower(var.agent_runtime_name)}_repository_${random_id.unique_suffix.hex}"

  #checkov:skip=CKV_AWS_51:Image tag is reused for latest deployments
  image_tag_mutability = "MUTABLE"
  force_delete         = true

  image_scanning_configuration {
    scan_on_push = true
  }

  tags = var.tags
}

# ECR Repository Policy
resource "aws_ecr_repository_policy" "agent_core_ecr_policy" {
  repository = aws_ecr_repository.agent_core_repository.name

  policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Sid    = "AllowPushPull"
        Effect = "Allow"
        Principal = {
          AWS = "arn:aws:iam::${local.aws_account_id}:root"
        }
        Action = [
          "ecr:GetDownloadUrlForLayer",
          "ecr:BatchGetImage",
          "ecr:BatchCheckLayerAvailability",
          "ecr:PutImage",
          "ecr:InitiateLayerUpload",
          "ecr:UploadLayerPart",
          "ecr:CompleteLayerUpload"
        ]
      }
    ]
  })
}

# IAM Role for Agent Core Runtime
resource "aws_iam_role" "agent_core_runtime_role" {
  name = "${var.agent_runtime_name}-AgentCoreRuntimeRole-${random_id.unique_suffix.hex}"

  assume_role_policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Sid    = "AgentCoreAssumeRolePolicy"
        Effect = "Allow"
        Principal = {
          Service = "bedrock-agentcore.amazonaws.com"
        }
        Action = "sts:AssumeRole"
        Condition = {
          StringEquals = {
            "aws:SourceAccount" = local.aws_account_id
          }
          ArnLike = {
            "aws:SourceArn" = "arn:aws:bedrock-agentcore:${local.aws_region}:${local.aws_account_id}:*"
          }
        }
      }
    ]
  })

  tags = var.tags
}

# IAM Policy for Agent Core Runtime
resource "aws_iam_policy" "agent_core_runtime_policy" {
  name        = "${var.agent_runtime_name}-QueryAgentPolicy-${random_id.unique_suffix.hex}"
  description = "Restricted policy for Agent"

  policy = jsonencode({
    Version = "2012-10-17"
    Statement = concat([
      {
        Sid    = "ECRImageAccess"
        Effect = "Allow"
        Action = [
          "ecr:BatchGetImage",
          "ecr:GetDownloadUrlForLayer"
        ]
        Resource = [
          aws_ecr_repository.agent_core_repository.arn
        ]
      },
      {
        Sid    = "ECRTokenAccess"
        Effect = "Allow"
        Action = [
          "ecr:GetAuthorizationToken"
        ]
        Resource = [
          "*"
        ]
      },
      {
        "Effect" : "Allow",
        "Action" : [
          "logs:DescribeLogStreams",
          "logs:CreateLogGroup"
        ],
        "Resource" : [
          "arn:aws:logs:${local.aws_region}:${local.aws_account_id}:log-group:/aws/bedrock-agentcore/runtimes/*"
        ]
      },
      {
        "Effect" : "Allow",
        "Action" : [
          "logs:DescribeLogGroups"
        ],
        "Resource" : [
          "arn:aws:logs:${local.aws_region}:${local.aws_account_id}:log-group:*"
        ]
      },
      {
        "Effect" : "Allow",
        "Action" : [
          "logs:CreateLogStream",
          "logs:PutLogEvents"
        ],
        "Resource" : [
          "arn:aws:logs:${local.aws_region}:${local.aws_account_id}:log-group:/aws/bedrock-agentcore/runtimes/*:log-stream:*"
        ]
      },
      {
        "Effect" : "Allow",
        "Action" : [
          "xray:PutTraceSegments",
          "xray:PutTelemetryRecords",
          "xray:GetSamplingRules",
          "xray:GetSamplingTargets"
        ],
        "Resource" : ["*"]
      },
      {
        "Effect" : "Allow",
        "Resource" : "*",
        "Action" : "cloudwatch:PutMetricData",
        "Condition" : {
          "StringEquals" : {
            "cloudwatch:namespace" : "bedrock-agentcore"
          }
        }
      },
      {
        "Sid" : "GetAgentAccessToken",
        "Effect" : "Allow",
        "Action" : [
          "bedrock-agentcore:GetWorkloadAccessToken",
          "bedrock-agentcore:GetWorkloadAccessTokenForJWT",
          "bedrock-agentcore:GetWorkloadAccessTokenForUserId"
        ],
        "Resource" : [
          "arn:aws:bedrock-agentcore:${local.aws_region}:${local.aws_account_id}:workload-identity-directory/default",
          "arn:aws:bedrock-agentcore:${local.aws_region}:${local.aws_account_id}:workload-identity-directory/default/workload-identity/*"
        ]
      }
    ], var.additional_iam_policy_statements)
  })

  tags = var.tags
}

# Attach the restricted policy to the role
resource "aws_iam_role_policy_attachment" "agent_core_policy" {
  role       = aws_iam_role.agent_core_runtime_role.name
  policy_arn = aws_iam_policy.agent_core_runtime_policy.arn
}

# Data source to get Docker image digest
data "external" "docker_digest" {
  program = ["sh", "-c", "echo '{\"digest\":\"'$(docker inspect ${var.docker_image_tag} --format '{{.Id}}')'\"}' "]
}

# Null resource for Docker publish
resource "null_resource" "docker_publish" {
  triggers = {
    docker_digest    = data.external.docker_digest.result.digest
    repository_url   = aws_ecr_repository.agent_core_repository.repository_url
    docker_image_tag = var.docker_image_tag
  }

  provisioner "local-exec" {
    command = <<-EOT
      # Get ECR login token
      aws ecr get-login-password --region ${local.aws_region} | docker login --username AWS --password-stdin ${self.triggers.repository_url}

      # Tag the image
      docker tag ${self.triggers.docker_image_tag} ${self.triggers.repository_url}:latest

      # Push the image
      docker push ${self.triggers.repository_url}:latest
    EOT
  }

  depends_on = [aws_ecr_repository_policy.agent_core_ecr_policy]
}

# Bedrock AgentCore Agent Runtime
resource "aws_bedrockagentcore_agent_runtime" "agent_runtime" {
  agent_runtime_name = "${var.agent_runtime_name}_${random_id.unique_suffix.hex}"
  description        = "Agent Runtime for ${var.agent_runtime_name}"
  role_arn           = aws_iam_role.agent_core_runtime_role.arn

  agent_runtime_artifact {
    container_configuration {
      container_uri = "${aws_ecr_repository.agent_core_repository.repository_url}:latest"
    }
  }

  environment_variables = length(var.env) > 0 ? var.env : null

  dynamic "authorizer_configuration" {
    for_each = var.authorizer_configuration != null && var.authorizer_configuration.custom_jwt_authorizer != null ? [var.authorizer_configuration.custom_jwt_authorizer] : []
    content {
      custom_jwt_authorizer {
        discovery_url    = authorizer_configuration.value.discovery_url
        allowed_audience = authorizer_configuration.value.allowed_audience
        allowed_clients  = authorizer_configuration.value.allowed_clients
      }
    }
  }

  network_configuration {
    network_mode = "PUBLIC"
  }

  protocol_configuration {
    server_protocol = var.server_protocol
  }

  tags = var.tags

  depends_on = [
    null_resource.docker_publish,
    aws_iam_role_policy.agent_core_runtime_policy
  ]
}

# Outputs
output "agent_core_runtime_role_arn" {
  description = "ARN of the Agent Core Runtime IAM role"
  value       = aws_iam_role.agent_core_runtime_role.arn
}

output "agent_core_runtime_role_name" {
  description = "Name of the Agent Core Runtime IAM role"
  value       = aws_iam_role.agent_core_runtime_role.name
}

output "agent_runtime_name" {
  description = "Name of the deployed agent runtime"
  value       = aws_bedrockagentcore_agent_runtime.agent_runtime.agent_runtime_name
}

output "agent_core_runtime_arn" {
  description = "ARN of the Bedrock Agent Core runtime"
  value       = aws_bedrockagentcore_agent_runtime.agent_runtime.agent_runtime_arn
}

output "agent_runtime_id" {
  description = "ID of the Bedrock Agent Core runtime"
  value       = aws_bedrockagentcore_agent_runtime.agent_runtime.agent_runtime_id
}

output "agent_runtime_version" {
  description = "Version of the Bedrock Agent Core runtime"
  value       = aws_bedrockagentcore_agent_runtime.agent_runtime.agent_runtime_version
}
